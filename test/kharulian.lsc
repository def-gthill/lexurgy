Feature Type(cons, vowel)

Feature Place(labial, alveolar, velar)
Feature Palatal(*unpalatal, palatal)
Feature Manner(stop, affricate, fricative, nasal, lateral)
Feature Voicing(unvoiced, voiced)

Feature Height(low, lowmid, mid, high)
Feature Depth(front, central, back)
Feature Rounding(*normal, crossround)
Feature Realness(*real, fake)
Feature Length(*short, glide, long)
Feature Stress(*unstressed, stressed)

Diacritic ́  (floating) [stressed]
Diacritic ː (floating) [long]
Diacritic ʲ [palatal]

Symbol p [cons unvoiced labial stop]
Symbol b [cons voiced labial stop]
Symbol t [cons unvoiced alveolar stop]
Symbol d [cons voiced alveolar stop]
Symbol c [cons unvoiced velar palatal stop]
Symbol ɟ [cons voiced velar palatal stop]
Symbol k [cons unvoiced velar stop]
Symbol g [cons voiced velar stop]
Symbol t͜s [cons unvoiced alveolar affricate]
Symbol d͜z [cons voiced alveolar affricate]
Symbol tɕ [cons unvoiced alveolar palatal affricate]
Symbol dʑ [cons voiced alveolar palatal affricate]
Symbol ɸ [cons unvoiced labial fricative]
Symbol β [cons voiced labial fricative]
Symbol s [cons unvoiced alveolar fricative]
Symbol z [cons voiced alveolar fricative]
Symbol ç [cons unvoiced velar palatal fricative]
Symbol ʝ [cons voiced velar palatal fricative]
Symbol x [cons unvoiced velar fricative]
Symbol ɣ [cons voiced velar fricative]
Symbol m [cons labial nasal]
Symbol n [cons alveolar nasal]
Symbol ɲ [cons velar palatal nasal]
Symbol l [cons alveolar lateral]
Symbol r [cons alveolar]

Symbol a [vowel low central]
Symbol ə [vowel mid central fake]
Symbol ɛ [vowel lowmid front]
Symbol e [vowel mid front]
Symbol i [vowel high front]
Symbol o [vowel mid back]
Symbol ɔ [vowel lowmid back]
Symbol u [vowel high back]
Symbol y [vowel high front crossround]
Symbol j [vowel high front glide]
Symbol w [vowel high back glide]

Class liquid {l, r, w, j}

Deromanizer:
    x => ç
    ñ => ɲ
    y => j

# Find the surface form of the Proto-Kharulian word

glides:
    [glide] => [long] / {[cons] _ [cons], [cons] _ $, $ _ [cons], _ [glide] [vowel], [vowel] [glide] _}
    [glide] [glide]$1 * => [long] $1 ə // _ [vowel]

single-consonant:
    * => ə / $ [cons] _ $

break-up-clusters:
    * => ə / {$ {[cons], [glide]} _ [cons], [stop] _ [stop]}
    * => ə / {{[cons], [glide]} [cons] _ [cons], [vowel] [cons] _ [cons] $}

palatal-assimilation:
    [alveolar !lateral] => [velar palatal] / _ [palatal]
    [palatal] => [alveolar unpalatal] / _ [alveolar]

glottal-insertion:
    * => ʔ / [vowel !glide] _ [vowel !glide]

voice-intervocalic:
    [unvoiced] => [voiced] / [vowel !glide] _ [vowel !glide]

stress [vowel]:
    [real !glide] => [stressed] / _ {[fake], [glide]}* $

# Now start evolving it

metathesis:
    [stop]$1 {[fricative], [nasal]}$2 => $2 $1
    [fricative]$1 [nasal]$2 => $2 $1
#    @liquid$1 [cons]$2 => $2 $1

early-glide-coalescence:
    {[short high front] j, j [short high front]} => {iː *, * iː}
    {[short high back] w, w [short high back]} => {uː *, * uː}

u-fronting:
    [high back long] => [front short crossround]

echo-vowels [vowel]:
    ə => [$Height $Depth $Rounding real] / [$Height $Depth $Rounding real !glide] [glide]* _

boundary-vowels [vowel]:
    ə => a / {$ _, _ $}
    Then: a => [stressed] / _ $
    Then: [stressed] => [unstressed] / [stressed] [unstressed]* _

schwa-loss:
    ə => * / _ [cons] [real] // [cons] [cons] _

# Semiev

stress-movement [vowel]:
    [] => [stressed] / [unstressed] [unstressed] [unstressed] _ $
    [stressed] => [unstressed] / _ []* [stressed]

cluster-reduction:
    [alveolar !lateral] => [velar palatal] / _ [palatal]
    [palatal] => [alveolar unpalatal] / _ [alveolar]
    @liquid => * / {@liquid _ [cons], [cons] _ @liquid}

frication:
    {[stop alveolar], [stop]} => {[affricate], [fricative]} / {_ [low central], _ [stop]} // [cons] _

umlaut [vowel]:
    {a, á, e, é, u, ú} => {e, é, i, í, y, ý} / _ [glide]* [high front !glide]

palatalization:
    {t, d, s, z, n, [cons]} => {tɕ, dʑ, ç, ʝ, ɲ, [palatal]} / _ [cons]* [front]
    [velar palatal] => [unpalatal] / _ w
    Then: [glide] => * / [cons] _

glide-fortition:
    j => ʝ // _ [cons]
    w => ɣ / _ [front]
    w => β // _ [cons]

vowel-shift:
    {y, ý, iː, íː, i, í, e, é} => {i, í, i, í, e, é, ɛ, ɛ́}

glide-coalescence:
    [low] j => [front mid long] *
    [vowel] j => [front high long] *
    {[low], [front mid]} w => [back mid long] *
    [vowel] w => [back high long] *

glottal-loss:
    {[high], [mid real], [lowmid]} => {[mid], [lowmid], [low central]} / _ ʔ
    Then: ʔ => *

vowel-coalescence:
    [$Height $Depth] [$Height $Depth stressed] => * [long]
    [$Height $Depth] [$Height $Depth] => [long] *
    Then: {{ɛe, eɛ}, {ɔo, oɔ}} => {eː, oː}
    Then: {{ei, ie}, {ou, uo}} => {iː, uː}
    Then: {aɛ, ɛa, aɔ, ɔa} => aː
    Then: {{ai, ia}, {au, ua}} => {eː, oː}
    Then: [lowmid back] => [mid]

stress-raising:
    {[lowmid stressed], [mid stressed]} => {[mid], [high]}

rhotacization:
    z => r / [vowel] _ [vowel]

remove-stress [vowel]:
    [stressed] => [unstressed] / _ $

Romanizer:
    [long]$1 * => $1 $1
    Then: [long] => [short]
    Then:
    ɟ => j
    t͜s => ts
    d͜z => dz
#    ts => t's
#    dz => d'z
    tɕ => ch
    dʑ => dj
    ɸ => f
    ç => sh
    x => kh
    β => v
    ʝ => zh
    ɣ => gh
    ɲ => ñ
    ŋ => ng
    ə => y
    ɛ => è
    {á, é, í, ú} => {á, é, í, ú}
    {pʲ, bʲ, ɸʲ, βʲ, mʲ, lʲ, rʲ} => {p, b, f, v, m, l, r} / _ [cons]* [front]
    {pʲ, bʲ, ɸʲ, βʲ, mʲ, lʲ, rʲ} => {py, by, fy, vy, my, ly, ry} / _ [vowel !front]
    {p, b, ɸ, β, m, l, r} => {p', b', f', v', m', l', r'} / _ [front]
